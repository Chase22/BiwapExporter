plugins {
    id 'application'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
    id 'org.hidetake.ssh' version '2.10.1'
}

group 'io.github.chase22'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://dl.bintray.com/kotlin/exposed"
    }
}

application {
    mainClassName = "io.github.chase22.nina.Main"
}

jar.enabled = false
jar {
    manifest {
        attributes 'Main-Class': "io.github.chase22.nina.Main"
    }
}

remotes {
    pi {
        host = '192.168.2.200'
        user = 'biwap'
        identity = file('/home/lukas/.ssh/id_rsa')
    }
}

final deployHomeDir = '/home/biwap'
final serviceDir = "${deployHomeDir}/.config/systemd/user"

task deployService {
    doLast {
        ssh.run {
            session(remotes.pi) {
                try {
                    String remoteContent = get from: "$serviceDir/BiwapExporter.service"
                    if (!file("${project.projectDir}/BiwapExporter.service").text.contentEquals(remoteContent)) {
                        println("Changes in Servicefile detected. Copying new File...")
                        put from: "${project.projectDir}/BiwapExporter.service", into: serviceDir
                        execute "systemctl --user daemon-reload"
                    }
                } catch (Exception e) {
                    if (e.hasProperty('error') && e.error as String == 'SSH_FX_NO_SUCH_FILE') {
                        println("Servicefile not found. Copying...")
                        put from: "${project.projectDir}/BiwapExporter.service", into: serviceDir
                    } else {
                        throw new TaskExecutionException(deployService, e)
                    }
                }
            }
        }
    }
}

task deploy {
    dependsOn(assemble)
    dependsOn(deployService)
    doLast {
        ssh.run {
            session(remotes.pi) {
                put from: "${project.buildDir}/libs/NinaExporter-1.0-all.jar", into: deployHomeDir
                execute 'systemctl --user restart BiwapExporter.service'
                execute 'systemctl --user status BiwapExporter.service'
            }
        }
    }
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.0'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.9.7'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.10.1'

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.12.0'

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.0.1'

    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    implementation group: 'org.jetbrains.exposed', name: 'exposed', version: '0.17.7'
    implementation group: 'com.h2database', name: 'h2', version: '1.4.197'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}
